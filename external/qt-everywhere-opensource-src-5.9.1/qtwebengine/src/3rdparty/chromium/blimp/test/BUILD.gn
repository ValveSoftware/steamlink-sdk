# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")

source_set("run_all_unittests") {
  testonly = true

  sources = [
    "run_all_unittests.cc",
  ]

  deps = [
    "//base/test:test_support",
    "//blimp/client/public:public_headers",
    "//mojo/edk/system",
    "//ui/gfx:gfx",
  ]

  if (is_android) {
    deps += [ "//ui/android" ]
  }
}

source_set("support") {
  testonly = true

  sources = [
    "support/compositor/picture_cache_test_support.cc",
    "support/compositor/picture_cache_test_support.h",
  ]

  deps = [
    "//ui/gfx",
    "//ui/gfx/geometry",
  ]

  public_deps = [
    "//blimp/common",
    "//cc",
    "//skia",
    "//testing/gmock",
  ]
}
