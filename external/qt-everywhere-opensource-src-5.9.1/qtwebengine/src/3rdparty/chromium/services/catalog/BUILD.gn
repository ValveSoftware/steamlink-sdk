# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//services/service_manager/public/cpp/service.gni")
import("//services/service_manager/public/service_manifest.gni")
import("//testing/test.gni")

group("catalog") {
  testonly = true
  deps = [
    ":lib",
  ]
}

source_set("lib") {
  sources = [
    "catalog.cc",
    "catalog.h",
    "constants.cc",
    "constants.h",
    "entry.cc",
    "entry.h",
    "instance.cc",
    "instance.h",
    "manifest_provider.h",
    "reader.cc",
    "reader.h",
    "store.cc",
    "store.h",
    "types.h",
  ]

  deps = [
    "//base",
    "//components/filesystem:lib",
    "//services/catalog/public/interfaces",
    "//services/service_manager/public/cpp",
  ]

  public_deps = [
    # directory.mojom.h is #included by catalog.h
    "//components/filesystem/public/interfaces",
  ]

  data_deps = [
    ":manifest",
  ]
}

service_manifest("manifest") {
  name = "catalog"
  source = "manifest.json"
}

source_set("unittests") {
  testonly = true
  sources = [
    "entry_unittest.cc",
  ]
  data = [
    "//services/catalog/data/",
  ]
  deps = [
    ":lib",
    "//base",
    "//services/service_manager/public/cpp",
    "//testing/gtest",
  ]
}
