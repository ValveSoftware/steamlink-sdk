# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("cpp") {
  public_deps = [
    ":sources",
  ]
}

# TODO(rockot): Rename this to "cpp".
static_library("sources") {
  sources = [
    "connect.h",
    "connection.h",
    "connector.h",
    "identity.h",
    "interface_binder.h",
    "interface_factory.h",
    "interface_factory_impl.h",
    "interface_provider.h",
    "interface_provider_spec.h",
    "interface_registry.h",
    "lib/callback_binder.cc",
    "lib/callback_binder.h",
    "lib/connection_impl.cc",
    "lib/connection_impl.h",
    "lib/connector_impl.cc",
    "lib/connector_impl.h",
    "lib/identity.cc",
    "lib/interface_factory_binder.h",
    "lib/interface_provider.cc",
    "lib/interface_provider_spec.cc",
    "lib/interface_registry.cc",
    "lib/service.cc",
    "lib/service_context.cc",
    "lib/service_context_ref.cc",
    "lib/service_info.cc",
    "lib/service_runner.cc",
    "service.h",
    "service_context.h",
    "service_context_ref.h",
    "service_info.h",
    "service_runner.h",
  ]

  public_deps = [
    "//base",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//services/service_manager/public/interfaces",
    "//url",
  ]
}

if (!is_ios) {
  # This cannot be a static library because it needs to export the symbols from
  # initialize_base_and_icu.cc.
  source_set("application_support") {
    sources = [
      "lib/init_commandline.cc",
      "lib/initialize_base_and_icu.cc",
    ]

    deps = [
      "//base",
      "//base:i18n",
      "//mojo/public/c/system",
    ]
  }

  static_library("service_test_support") {
    testonly = true
    sources = [
      "lib/service_test.cc",
      "service_test.h",
    ]

    public_deps = [
      ":cpp",
      "//testing/gtest",
    ]

    deps = [
      "//base",
      "//base/test:test_support",
      "//mojo/public/cpp/bindings",
      "//mojo/public/cpp/system",
      "//services/service_manager/background:lib",
      "//services/service_manager/public/interfaces:interfaces_cpp_sources",
    ]

    data_deps = []
  }
}
