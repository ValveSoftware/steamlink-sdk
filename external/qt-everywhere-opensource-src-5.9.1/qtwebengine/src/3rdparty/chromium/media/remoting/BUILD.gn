# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")
import("//third_party/protobuf/proto_library.gni")

proto_library("media_remoting_proto") {
  proto_out_dir = "media/remoting"
  sources = [
    "proto/remoting_rpc_message.proto",
  ]
}

source_set("rpc") {
  sources = [
    "rpc/proto_enum_utils.cc",
    "rpc/proto_enum_utils.h",
    "rpc/proto_utils.cc",
    "rpc/proto_utils.h",
    "rpc/rpc_broker.cc",
    "rpc/rpc_broker.h",
  ]

  deps = [
    "//base",
    "//media",
  ]

  public_deps = [
    ":media_remoting_proto",
  ]
}

source_set("remoting") {
  sources = [
    "remote_demuxer_stream_adapter.cc",
    "remote_demuxer_stream_adapter.h",
    "remote_renderer_impl.cc",
    "remote_renderer_impl.h",
    "remoting_cdm.cc",
    "remoting_cdm.h",
    "remoting_cdm_context.cc",
    "remoting_cdm_context.h",
    "remoting_cdm_controller.cc",
    "remoting_cdm_controller.h",
    "remoting_cdm_factory.cc",
    "remoting_cdm_factory.h",
    "remoting_interstitial_ui.cc",
    "remoting_interstitial_ui.h",
    "remoting_renderer_controller.cc",
    "remoting_renderer_controller.h",
    "remoting_renderer_factory.cc",
    "remoting_renderer_factory.h",
    "remoting_sink_observer.cc",
    "remoting_sink_observer.h",
    "remoting_source_impl.cc",
    "remoting_source_impl.h",
  ]
  deps = [
    ":rpc",
    "//base",
    "//media",
    "//media/mojo/interfaces:remoting",
    "//mojo/public/cpp/bindings",
    "//skia",
    "//ui/gfx",
  ]
}

source_set("media_remoting_tests") {
  testonly = true
  sources = [
    "fake_remoting_controller.cc",
    "fake_remoting_controller.h",
    "fake_remoting_demuxer_stream_provider.cc",
    "fake_remoting_demuxer_stream_provider.h",
    "remote_demuxer_stream_adapter_unittest.cc",
    "remote_renderer_impl_unittest.cc",
    "remoting_renderer_controller_unittest.cc",
    "rpc/proto_utils_unittest.cc",
    "rpc/rpc_broker_unittest.cc",
  ]

  deps = [
    ":remoting",
    ":rpc",
    "//base",
    "//base/test:test_support",
    "//media",
    "//media/base:test_support",
    "//media/mojo/interfaces:remoting",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/gfx:test_support",
    "//ui/gfx/geometry",
    "//url",
  ]
}

test("media_remoting_unittests") {
  deps = [
    ":media_remoting_tests",
    "//media/test:run_all_unittests",
  ]
}
