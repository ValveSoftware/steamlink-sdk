# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/rules.gni")
}
import("//build/config/features.gni")

source_set("browser") {
  sources = [
    "feedback.cc",
    "feedback.h",
    "feedback_sender.cc",
    "feedback_sender.h",
    "misspelling.cc",
    "misspelling.h",
    "pref_names.cc",
    "pref_names.h",
    "spellcheck_action.cc",
    "spellcheck_action.h",
    "spellcheck_dictionary.h",
    "spellcheck_host_metrics.cc",
    "spellcheck_host_metrics.h",
    "spellcheck_message_filter_platform.h",
    "spellcheck_message_filter_platform_android.cc",
    "spellcheck_platform.h",
    "spellcheck_platform_android.cc",
    "spellcheck_platform_mac.mm",
    "spellchecker_session_bridge_android.cc",
    "spellchecker_session_bridge_android.h",
    "spelling_service_client.cc",
    "spelling_service_client.h",
    "word_trimmer.cc",
    "word_trimmer.h",
  ]

  public_deps = [
    "//base",
    "//components/spellcheck:build_features",
    "//components/spellcheck/common",
  ]
  deps = [
    "//components/data_use_measurement/core",
    "//components/prefs",
    "//components/user_prefs",
    "//content/public/browser",
    "//content/public/common",
    "//google_apis",
    "//net",
  ]
  if (use_qt) {
    deps -= [
      "//components/data_use_measurement/core",
    ]
  }

  if (is_android) {
    sources += [
      "android/component_jni_registrar.cc",
      "android/component_jni_registrar.h",
    ]
    deps += [ "android:jni_headers" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "feedback_sender_unittest.cc",
    "feedback_unittest.cc",
    "misspelling_unittest.cc",
    "spellcheck_action_unittest.cc",
    "spellcheck_host_metrics_unittest.cc",
    "spellcheck_platform_mac_unittest.cc",
    "word_trimmer_unittest.cc",
  ]

  deps = [
    ":browser",
    "//base",
    "//base/test:test_support",
    "//components/spellcheck/common",
    "//components/variations",
    "//content/test:test_support",
    "//net:test_support",
    "//testing/gtest",
  ]
}
