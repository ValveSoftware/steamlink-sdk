# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

static_library("arc") {
  sources = [
    "arc_bridge_host_impl.cc",
    "arc_bridge_host_impl.h",
    "arc_bridge_service_impl.cc",
    "arc_bridge_service_impl.h",
    "arc_features.cc",
    "arc_features.h",
    "arc_service_manager.cc",
    "arc_service_manager.h",
    "arc_session.cc",
    "arc_session.h",
    "audio/arc_audio_bridge.cc",
    "audio/arc_audio_bridge.h",
    "bluetooth/arc_bluetooth_bridge.cc",
    "bluetooth/arc_bluetooth_bridge.h",
    "bluetooth/bluetooth_struct_traits.cc",
    "bluetooth/bluetooth_struct_traits.h",
    "bluetooth/bluetooth_type_converters.cc",
    "bluetooth/bluetooth_type_converters.h",
    "boot_phase_monitor/arc_boot_phase_monitor_bridge.cc",
    "boot_phase_monitor/arc_boot_phase_monitor_bridge.h",
    "clipboard/arc_clipboard_bridge.cc",
    "clipboard/arc_clipboard_bridge.h",
    "crash_collector/arc_crash_collector_bridge.cc",
    "crash_collector/arc_crash_collector_bridge.h",
    "ime/arc_ime_bridge.h",
    "ime/arc_ime_bridge_impl.cc",
    "ime/arc_ime_bridge_impl.h",
    "ime/arc_ime_service.cc",
    "ime/arc_ime_service.h",
    "intent_helper/activity_icon_loader.cc",
    "intent_helper/activity_icon_loader.h",
    "intent_helper/arc_intent_helper_bridge.cc",
    "intent_helper/arc_intent_helper_bridge.h",
    "intent_helper/font_size_util.cc",
    "intent_helper/font_size_util.h",
    "intent_helper/intent_constants.cc",
    "intent_helper/intent_constants.h",
    "intent_helper/intent_filter.cc",
    "intent_helper/intent_filter.h",
    "intent_helper/link_handler_model_impl.cc",
    "intent_helper/link_handler_model_impl.h",
    "intent_helper/local_activity_resolver.cc",
    "intent_helper/local_activity_resolver.h",
    "intent_helper/page_transition_util.cc",
    "intent_helper/page_transition_util.h",
    "kiosk/arc_kiosk_bridge.cc",
    "kiosk/arc_kiosk_bridge.h",
    "metrics/arc_metrics_service.cc",
    "metrics/arc_metrics_service.h",
    "metrics/oom_kills_histogram.h",
    "metrics/oom_kills_monitor.cc",
    "metrics/oom_kills_monitor.h",
    "net/arc_net_host_impl.cc",
    "net/arc_net_host_impl.h",
    "obb_mounter/arc_obb_mounter_bridge.cc",
    "obb_mounter/arc_obb_mounter_bridge.h",
    "power/arc_power_bridge.cc",
    "power/arc_power_bridge.h",
    "storage_manager/arc_storage_manager.cc",
    "storage_manager/arc_storage_manager.h",
    "user_data/arc_user_data_service.cc",
    "user_data/arc_user_data_service.h",
  ]

  public_deps = [
    ":arc_base",
  ]

  deps = [
    "//ash:ash",
    "//ash/public/interfaces",
    "//base",
    "//chromeos",
    "//chromeos:power_manager_proto",
    "//components/exo",
    "//components/google/core/browser",
    "//components/onc",
    "//components/prefs",
    "//components/signin/core/account_id",
    "//components/user_manager",
    "//device/bluetooth",
    "//google_apis",
    "//ipc:ipc",
    "//mojo/edk/system",
    "//skia",
    "//third_party/re2:re2",
    "//ui/arc",
    "//ui/aura",
    "//ui/base:base",
    "//ui/base/ime",
    "//ui/display:display",
    "//ui/events",
    "//ui/events:dom_keycode_converter",
    "//ui/keyboard:keyboard",
    "//url:url",
  ]
}

static_library("arc_base") {
  sources = [
    "arc_bridge_service.cc",
    "arc_bridge_service.h",
    "arc_service.cc",
    "arc_service.h",
    "instance_holder.h",
  ]

  deps = [
    "//base",
    "//chromeos",
  ]

  public_deps = [
    ":arc_bindings",
  ]
}

# TODO(crbug.com/662510): Remove this altogether.
mojom("arc_bindings_old_types") {
  sources = [
    "common/bitmap.mojom",
    "common/bluetooth.mojom",
    "common/net.mojom",
  ]

  public_deps = [
    "//mojo/common:common_custom_types",
    "//ui/gfx/geometry/mojo",
  ]

  use_new_wrapper_types = false
}

mojom("arc_bindings") {
  sources = [
    "common/app.mojom",
    "common/arc_bridge.mojom",
    "common/audio.mojom",
    "common/auth.mojom",
    "common/boot_phase_monitor.mojom",
    "common/clipboard.mojom",
    "common/crash_collector.mojom",
    "common/enterprise_reporting.mojom",
    "common/file_system.mojom",
    "common/ime.mojom",
    "common/intent_helper.mojom",
    "common/kiosk.mojom",
    "common/metrics.mojom",
    "common/notifications.mojom",
    "common/obb_mounter.mojom",
    "common/policy.mojom",
    "common/power.mojom",
    "common/print.mojom",
    "common/process.mojom",
    "common/scale_factor.mojom",
    "common/screen_rect.mojom",
    "common/storage_manager.mojom",
    "common/tts.mojom",
    "common/video.mojom",
    "common/video_accelerator.mojom",
    "common/wallpaper.mojom",
  ]

  public_deps = [
    ":arc_bindings_old_types",
    "//mojo/common:common_custom_types",
  ]
}

static_library("arc_test_support") {
  testonly = true
  sources = [
    "test/fake_app_instance.cc",
    "test/fake_app_instance.h",
    "test/fake_arc_bridge_service.cc",
    "test/fake_arc_bridge_service.h",
    "test/fake_arc_session.cc",
    "test/fake_arc_session.h",
    "test/fake_bluetooth_instance.cc",
    "test/fake_bluetooth_instance.h",
    "test/fake_intent_helper_instance.cc",
    "test/fake_intent_helper_instance.h",
    "test/fake_notifications_instance.cc",
    "test/fake_notifications_instance.h",
    "test/fake_policy_instance.cc",
    "test/fake_policy_instance.h",
  ]

  public_deps = [
    ":arc",
  ]

  deps = [
    "//base",
    "//mojo/common:common_base",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "arc_bridge_service_unittest.cc",
    "bluetooth/arc_bluetooth_bridge_unittest.cc",
    "bluetooth/bluetooth_struct_traits_unittest.cc",
    "bluetooth/bluetooth_type_converters_unittest.cc",
    "ime/arc_ime_service_unittest.cc",
    "intent_helper/activity_icon_loader_unittest.cc",
    "intent_helper/arc_intent_helper_bridge_unittest.cc",
    "intent_helper/font_size_util_unittest.cc",
    "intent_helper/intent_filter_unittest.cc",
    "intent_helper/link_handler_model_impl_unittest.cc",
    "intent_helper/local_activity_resolver_unittest.cc",
    "intent_helper/page_transition_util_unittest.cc",
  ]

  deps = [
    ":arc_test_support",
    "//base",
    "//chromeos",
    "//device/bluetooth",
    "//mojo/public/cpp/system:system",
    "//testing/gtest",
    "//ui/aura",
    "//ui/aura:test_support",
    "//ui/base:test_support",
    "//ui/base/ime",
    "//ui/events",
    "//ui/events:dom_keycode_converter",
    "//url:url",
  ]
}
