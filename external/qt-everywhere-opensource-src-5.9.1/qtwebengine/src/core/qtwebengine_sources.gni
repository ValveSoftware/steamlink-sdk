import("//build/config/features.gni")
import("//components/spellcheck/spellcheck_build_features.gni")
import("//printing/features/features.gni")

source_set("qtwebengine_spellcheck_sources") {
  sources = [
    "//chrome/browser/spellchecker/spellcheck_custom_dictionary.cc",
    "//chrome/browser/spellchecker/spellcheck_custom_dictionary.h",
    "//chrome/browser/spellchecker/spellcheck_factory.cc",
    "//chrome/browser/spellchecker/spellcheck_factory.h",
    "//chrome/browser/spellchecker/spellcheck_hunspell_dictionary.cc",
    "//chrome/browser/spellchecker/spellcheck_hunspell_dictionary.h",
    "//chrome/browser/spellchecker/spellcheck_message_filter.cc",
    "//chrome/browser/spellchecker/spellcheck_message_filter.h",
    "//chrome/browser/spellchecker/spellcheck_service.cc",
    "//chrome/browser/spellchecker/spellcheck_service.h",
  ]
  deps = [
    "//components/spellcheck/browser",
    "//components/spellcheck/renderer",
    "//third_party/WebKit/public:blink",
    "//third_party/hunspell",
  ]
  if (is_mac && use_browser_spellchecker) {
    sources += [
        "//chrome/browser/spellchecker/spellcheck_message_filter_platform.h",
        "//chrome/browser/spellchecker/spellcheck_message_filter_platform_mac.cc",
    ]
  }
}

source_set("qtwebengine_sources") {
  include_dirs = [
    "//skia/config",
    "//third_party/skia/include/core"
  ]
  deps = [
      "//extensions/features:features",
  ]
  sources = [
    "//chrome/browser/media/webrtc/desktop_media_list.h",
    "//chrome/browser/media/webrtc/desktop_streams_registry.cc",
    "//chrome/browser/media/webrtc/desktop_streams_registry.h",
    "//chrome/common/chrome_switches.cc",
    "//chrome/common/chrome_switches.h",
    "//components/prefs/testing_pref_store.cc",
    "//components/prefs/testing_pref_store.h",
    "//extensions/common/constants.cc",
    "//extensions/common/constants.h",
    "//extensions/common/url_pattern.cc",
    "//extensions/common/url_pattern.h",
  ]

  if (enable_spellcheck) {
    sources += [
      "//chrome/common/pref_names.cc",
      "//chrome/common/pref_names.h",
    ]
    deps += [
      ":qtwebengine_spellcheck_sources",
      "//chrome/tools/convert_dict",
    ]
  }

  if (enable_plugins) {
      sources += [
        "//chrome/browser/renderer_host/pepper/pepper_flash_clipboard_message_filter.cc",
        "//chrome/browser/renderer_host/pepper/pepper_flash_clipboard_message_filter.h",
        "//chrome/renderer/pepper/pepper_flash_font_file_host.cc",
        "//chrome/renderer/pepper/pepper_flash_font_file_host.h",
        "//chrome/renderer/pepper/pepper_shared_memory_message_filter.cc",
        "//chrome/renderer/pepper/pepper_shared_memory_message_filter.h",
      ]

      deps += [
        # Need to depend on //content/ppapi_plugin, which is private, thus depending on parent.
        "//content",
      ]
  }

  if (enable_basic_printing || enable_print_preview) {
    sources += [
      "//chrome/browser/printing/printer_query.cc",
      "//chrome/browser/printing/printer_query.h",
      "//chrome/browser/printing/print_job.cc",
      "//chrome/browser/printing/print_job.h",
      "//chrome/browser/printing/print_job_manager.cc",
      "//chrome/browser/printing/print_job_manager.h",
      "//chrome/browser/printing/print_job_worker.cc",
      "//chrome/browser/printing/print_job_worker.h",
      "//chrome/browser/printing/print_job_worker_owner.cc",
      "//chrome/browser/printing/print_job_worker_owner.h",
    ]
    deps += [
      "//printing/features:features",
    ]
  }

  if (enable_pdf) {
      deps += [
         "//pdf",
         "//components/printing/browser",
         "//components/printing/renderer",
      ]
  }

}

