# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//remoting/remoting_srcs.gni")

source_set("signaling") {
  sources = rebase_path(remoting_srcs_gypi_values.remoting_signaling_sources,
                        ".",
                        "//remoting")

  configs += [
    "//build/config/compiler:no_size_t_to_int_warning",
    "//build/config/compiler:wexit_time_destructors",
  ]

  public_deps = [
    "//remoting/proto",
    "//third_party/libjingle",
  ]

  deps = [
    "//base",
    "//crypto",
    "//jingle:jingle_glue",
    "//net",
    "//remoting/base",
  ]

  if (is_nacl) {
    sources -= [
      "log_to_server.cc",
      "server_log_entry.cc",
      "xmpp_signal_strategy.cc",
    ]
  }
}

source_set("test_support") {
  testonly = true

  sources = [
    "fake_signal_strategy.cc",
    "fake_signal_strategy.h",
    "mock_signal_strategy.cc",
    "mock_signal_strategy.h",
  ]

  public_deps = [
    ":signaling",
    "//testing/gmock",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "iq_sender_unittest.cc",
    "jid_util_unittest.cc",
    "log_to_server_unittest.cc",
    "push_notification_subscriber_unittest.cc",
    "server_log_entry_unittest.cc",
    "server_log_entry_unittest.h",
    "xmpp_login_handler_unittest.cc",
    "xmpp_signal_strategy_unittest.cc",
    "xmpp_stream_parser_unittest.cc",
  ]

  deps = [
    ":test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
