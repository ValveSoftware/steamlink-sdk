# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//media/media_options.gni")

source_set("media") {
  sources = [
    "cast_browser_cdm_factory.cc",
    "cast_browser_cdm_factory.h",
    "media_pipeline_backend_factory.h",
    "media_pipeline_host.cc",
    "media_pipeline_host.h",
  ]

  if (mojo_media_host == "browser") {
    sources += [
      "cast_mojo_media_client.cc",
      "cast_mojo_media_client.h",
      "cast_renderer.cc",
      "cast_renderer.h",
    ]
  }

  if (mojo_media_host == "browser") {
    public_deps = [
      "//media/mojo/interfaces",
      "//media/mojo/services",
      "//services/shell/public/cpp",
    ]
  }

  deps = [
    "//base",
    "//chromecast/base",
    "//chromecast/common/media",
    "//chromecast/media",
    "//content/public/browser",
    "//media",
  ]

  if (!is_android) {
    sources += [
      "cma_message_filter_host.cc",
      "cma_message_filter_host.h",
    ]
  }
}
