# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")

assert(is_android)

# GYP target: chromecast.gyp:cast_shell_manifest
jinja_template("cast_shell_manifest") {
  input = "apk/AndroidManifest.xml.jinja2"
  output = "$root_gen_dir/cast_shell_manifest/AndroidManifest.xml"
}

# GYP target: n/a
android_resources("cast_shell_android_resources") {
  android_manifest = "$root_gen_dir/cast_shell_manifest/AndroidManifest.xml"
  android_manifest_dep = ":cast_shell_manifest"
  resource_dirs = [ "//chromecast/browser/android/apk/res" ]
}

# GYP target: chromecast.gyp:cast_shell_java
android_library("cast_shell_java") {
  java_src_dir = "//chromecast/browser/android/apk/src"
  java_files = [
    "$java_src_dir/org/chromium/chromecast/shell/CastApplication.java",
    "$java_src_dir/org/chromium/chromecast/shell/CastAudioManager.java",
    "$java_src_dir/org/chromium/chromecast/shell/CastBrowserHelper.java",
    "$java_src_dir/org/chromium/chromecast/shell/CastCrashHandler.java",
    "$java_src_dir/org/chromium/chromecast/shell/CastCrashUploader.java",
    "$java_src_dir/org/chromium/chromecast/shell/CastMetricsHelper.java",
    "$java_src_dir/org/chromium/chromecast/shell/CastShellActivity.java",
    "$java_src_dir/org/chromium/chromecast/shell/CastSysInfoAndroid.java",
    "$java_src_dir/org/chromium/chromecast/shell/CastWindowAndroid.java",
    "$java_src_dir/org/chromium/chromecast/shell/CastWindowManager.java",
  ]

  deps = [
    ":cast_shell_android_resources",
    "//base:base_java",
    "//chromecast/base:base_java",
    "//components/external_video_surface:java",
    "//content/public/android:content_java",
    "//media/base/android:media_java",
    "//net/android:net_java",

    # TODO(slan): We may need to pass this in as a parameter.
    "//third_party/android_tools:android_support_v13_java",
    "//ui/android:ui_java",
  ]
}
