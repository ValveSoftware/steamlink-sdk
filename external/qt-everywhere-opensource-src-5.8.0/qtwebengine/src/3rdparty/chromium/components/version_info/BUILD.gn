# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//chrome/version.gni")

declare_args() {
  use_unofficial_version_number = !is_chrome_branded
}

source_set("version_info") {
  sources = [
    "version_info.cc",
    "version_info.h",
  ]

  deps = [
    ":generate_version_info",
    "//base",
    "//components/strings",
  ]

  if (use_unofficial_version_number) {
    defines = [ "USE_UNOFFICIAL_VERSION_NUMBER" ]
    deps += [ "//ui/base" ]
  }
}

process_version("generate_version_info") {
  template_file = "version_info_values.h.version"
  output = "$target_gen_dir/version_info_values.h"
}
