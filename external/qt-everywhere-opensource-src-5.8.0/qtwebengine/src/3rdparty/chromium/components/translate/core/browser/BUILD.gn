# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")

source_set("browser") {
  sources = [
    "language_state.cc",
    "language_state.h",
    "page_translated_details.h",
    "translate_accept_languages.cc",
    "translate_accept_languages.h",
    "translate_browser_metrics.cc",
    "translate_browser_metrics.h",
    "translate_client.h",
    "translate_download_manager.cc",
    "translate_download_manager.h",
    "translate_driver.h",
    "translate_error_details.h",
    "translate_event_details.cc",
    "translate_event_details.h",
    "translate_experiment.cc",
    "translate_experiment.h",
    "translate_language_list.cc",
    "translate_language_list.h",
    "translate_manager.cc",
    "translate_manager.h",
    "translate_prefs.cc",
    "translate_prefs.h",
    "translate_script.cc",
    "translate_script.h",
    "translate_step.h",
    "translate_ui_delegate.cc",
    "translate_ui_delegate.h",
    "translate_url_fetcher.cc",
    "translate_url_fetcher.h",
    "translate_url_util.cc",
    "translate_url_util.h",
  ]

  deps = [
    "//base",
    "//base:i18n",
    "//components/data_use_measurement/core",
    "//components/language_usage_metrics",
    "//components/pref_registry",
    "//components/prefs",
    "//components/resources:components_resources",
    "//components/strings",
    "//components/translate/core/common",
    "//components/variations",
    "//google_apis",
    "//net",
    "//third_party/icu",
    "//ui/base",
    "//url",
  ]

  if (!use_aura) {
    sources += [
      "translate_infobar_delegate.cc",
      "translate_infobar_delegate.h",
    ]
    deps += [ "//components/infobars/core" ]
  }

  if (is_mac) {
    sources += [
      "options_menu_model.cc",
      "options_menu_model.h",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "language_state_unittest.cc",
    "mock_translate_driver.cc",
    "mock_translate_driver.h",
    "translate_browser_metrics_unittest.cc",
    "translate_language_list_unittest.cc",
    "translate_manager_unittest.cc",
    "translate_prefs_unittest.cc",
    "translate_script_unittest.cc",
    "translate_ui_delegate_unittest.cc",
  ]
  deps = [
    ":browser",
    "//base",
    "//components/infobars/core",
    "//components/pref_registry:test_support",
    "//components/prefs",
    "//components/translate/core/common",
    "//components/variations",
    "//net:test_support",
    "//testing/gtest",
  ]
}
