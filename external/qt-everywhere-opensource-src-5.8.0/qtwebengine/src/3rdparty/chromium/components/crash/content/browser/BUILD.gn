# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
}

source_set("browser") {
  sources = [
    "crash_dump_manager_android.cc",
    "crash_dump_manager_android.h",
    "crash_micro_dump_manager_android.cc",
    "crash_micro_dump_manager_android.h",
  ]

  deps = [
    "//base",
    "//breakpad:client",
    "//components/crash/content/app",
    "//content/public/browser",
    "//content/public/common",
  ]

  if (is_linux || is_android) {
    set_sources_assignment_filter([])

    # Want this file on both Linux and Android.
    sources += [
      "crash_handler_host_linux.cc",
      "crash_handler_host_linux.h",
    ]
    deps += [
      "//breakpad:client",
      "//components/crash/content/app:app_non_mac_win",
    ]
  }

  # This is not in the GYP build but this target includes breakpad client
  # headers, so add the dependency here.
  if (is_posix && !is_ios) {
    configs += [ "//breakpad:client_config" ]
    public_configs = [ "//breakpad:client_config" ]
  }
}
