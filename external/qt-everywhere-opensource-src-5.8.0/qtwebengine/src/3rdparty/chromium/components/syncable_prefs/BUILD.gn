# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")

source_set("syncable_prefs") {
  sources = [
    "pref_model_associator.cc",
    "pref_model_associator.h",
    "pref_model_associator_client.h",
    "pref_service_syncable.cc",
    "pref_service_syncable.h",
    "pref_service_syncable_factory.cc",
    "pref_service_syncable_factory.h",
    "pref_service_syncable_observer.h",
    "synced_pref_change_registrar.cc",
    "synced_pref_change_registrar.h",
    "synced_pref_observer.h",
  ]

  deps = [
    "//base",
    "//components/pref_registry",
    "//components/prefs",
    "//sync",
  ]

  if (enable_configuration_policy) {
    # This define is only used for compiling the .cc files in this target.
    defines = [ "SYNCABLE_PREFS_USE_POLICY" ]

    deps += [
      "//components/policy:policy_component_browser",
      "//components/policy:policy_component_common",
    ]
  }
}

source_set("test_support") {
  testonly = true
  sources = [
    "pref_service_mock_factory.cc",
    "pref_service_mock_factory.h",
    "testing_pref_service_syncable.cc",
    "testing_pref_service_syncable.h",
  ]

  deps = [
    ":syncable_prefs",
    "//base",
    "//components/pref_registry",
    "//components/prefs:test_support",
    "//testing/gtest",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "pref_model_associator_unittest.cc",
    "pref_service_syncable_unittest.cc",
  ]

  deps = [
    ":syncable_prefs",
    ":test_support",
    "//components/pref_registry",
    "//components/prefs",
    "//sync:test_support_sync_api",
    "//testing/gtest",
  ]
}
