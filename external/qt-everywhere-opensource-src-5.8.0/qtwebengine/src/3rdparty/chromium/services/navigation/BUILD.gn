# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application.gni")
import("//mojo/public/mojo_application_manifest.gni")
import("//testing/test.gni")
import("//tools/grit/repack.gni")

group("all") {
  testonly = true
  data_deps = [
    ":navigation",
    ":navigation_unittests",
  ]
}

executable("navigation") {
  testonly = true
  sources = [
    "main.cc",
  ]

  deps = [
    ":lib",
    ":pak",
    "//base",
    "//build/win:default_exe_manifest",
    "//components/mus/public/cpp",
    "//content",
    "//content/public/app:both",
    "//mojo/public/cpp/bindings",
    "//services/navigation/content_client",
    "//services/shell/runner:init",
    "//services/shell/runner/common",
    "//ui/views",
    "//ui/views/controls/webview",
    "//ui/views/mus",
  ]

  data_deps = [
    ":manifest",
    "//components/mus/test_wm",
  ]

  if (is_win) {
    configs -= [ "//build/config/win:console" ]
    configs += [ "//build/config/win:windowed" ]
    deps += [ "//sandbox" ]
  }
}

static_library("lib") {
  sources = [
    "navigation.cc",
    "navigation.h",
    "view_impl.cc",
    "view_impl.h",
  ]

  deps = [
    "//base",
    "//components/mus/public/cpp",
    "//content/public/browser",
    "//services/navigation/public/interfaces",
    "//services/shell/public/cpp",
    "//skia",
    "//ui/gfx/geometry/mojo",
    "//ui/views",
    "//ui/views/controls/webview",
    "//ui/views/mus",
  ]
}

test("navigation_unittests") {
  sources = [
    "navigation_unittest.cc",
  ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//services/navigation/public/interfaces",
    "//services/shell/public/cpp",
    "//services/shell/public/cpp:shell_test_support",
    "//services/shell/public/cpp/test:run_all_shelltests",
    "//testing/gtest",
  ]

  data_deps = [
    ":navigation",
    ":unittest_manifest",
    "//components/mus/test_wm",
  ]
}

mojo_application_manifest("manifest") {
  type = "exe"
  application_name = "navigation"
  source = "manifest.json"
}

mojo_application_manifest("unittest_manifest") {
  type = "exe"
  application_name = "navigation_unittests"
  source = "unittest_manifest.json"
}

repack("pak") {
  sources = [
    "$root_gen_dir/blink/devtools_resources.pak",
    "$root_gen_dir/blink/public/resources/blink_image_resources_100_percent.pak",
    "$root_gen_dir/blink/public/resources/blink_resources.pak",
    "$root_gen_dir/content/app/resources/content_resources_100_percent.pak",
    "$root_gen_dir/content/app/strings/content_strings_en-US.pak",
    "$root_gen_dir/content/browser/tracing/tracing_resources.pak",
    "$root_gen_dir/content/content_resources.pak",
    "$root_gen_dir/content/shell/shell_resources.pak",
    "$root_gen_dir/net/net_resources.pak",
    "$root_gen_dir/ui/resources/ui_resources_100_percent.pak",
    "$root_gen_dir/ui/resources/webui_resources.pak",
    "$root_gen_dir/ui/strings/app_locale_settings_en-US.pak",
    "$root_gen_dir/ui/strings/ui_strings_en-US.pak",
    "$root_gen_dir/ui/views/resources/views_resources_100_percent.pak",
  ]

  deps = [
    "//content:resources",
    "//content/app/resources",
    "//content/app/strings",
    "//content/browser/devtools:resources",
    "//content/browser/tracing:resources",
    "//content/shell:resources",
    "//net:net_resources",
    "//third_party/WebKit/public:image_resources",
    "//third_party/WebKit/public:resources",
    "//ui/resources",
    "//ui/strings",
    "//ui/views/resources",
  ]

  output = "$root_out_dir/navigation.pak"
}
