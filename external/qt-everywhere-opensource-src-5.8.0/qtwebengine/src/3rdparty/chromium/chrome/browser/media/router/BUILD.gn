# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#

import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

gypi_values = exec_script("//build/gypi_to_gn.py",
                          [ rebase_path("media_router.gypi") ],
                          "scope",
                          [ "media_router.gypi" ])

static_library("router") {
  deps = [
    "//base",
    "//chrome/common:constants",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//content/public/browser",
    "//url",
  ]
  sources = rebase_path(gypi_values.media_router_sources,
                        ".",
                        "//chrome/browser/media/router")
  if (!is_android) {
    deps += [
      ":mojo_bindings",
      "//extensions/browser",
      "//mojo/public/cpp/bindings",
    ]
    sources += rebase_path(gypi_values.media_router_non_android_sources,
                           ".",
                           "//chrome/browser/media/router")
  }
}

mojom("mojo_bindings") {
  sources = [
    "mojo/media_router.mojom",
  ]
}

source_set("test_support") {
  testonly = true
  deps = [
    ":router",
    "//chrome/test:test_support",
    "//testing/gmock",
  ]
  sources = rebase_path(gypi_values.media_router_test_support_sources,
                        ".",
                        "//chrome/browser/media/router")
  if (!is_android) {
    deps += [
      ":mojo_bindings",
      "//extensions/common",
    ]
    sources +=
        rebase_path(gypi_values.media_router_non_android_test_support_sources,
                    ".",
                    "//chrome/browser/media/router")
  }
}
