# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//content/utility/utility.gni")

# See //content/BUILD.gn for how this works.
group("utility") {
  if (is_component_build) {
    public_deps = [
      "//content",
    ]
  } else {
    public_deps = [
      ":utility_sources",
    ]
  }
}

source_set("utility_sources") {
  # External code should depend on via ":utility" above.
  visibility = [ "//content/*" ]

  sources = rebase_path(content_utility_gypi_values.public_utility_sources,
                        ".",
                        "//content")

  configs += [ "//content:content_implementation" ]

  deps = [
    "//base",
    "//content:export",
    "//content/public/child:child_sources",
    "//content/public/common:common_sources",
    "//content/utility",
    "//ipc",
  ]

  allow_circular_includes_from = [
    # This target is a pair with content/browser. They always go together and
    # include headers from each other.
    "//content/utility",
  ]
}
