# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//content/child/child.gni")

# See //content/BUILD.gn for how this works.
group("child") {
  if (is_component_build) {
    public_deps = [
      "//content",
    ]
  } else {
    public_deps = [
      ":child_sources",
    ]
    if (!is_android) {
      public_deps += [
        "//content/gpu",
        "//content/ppapi_plugin",
      ]
    }
  }
}

source_set("child_sources") {
  # External code should depend in via ":child" above.
  visibility = [ "//content/*" ]

  sources = rebase_path(content_child_gypi_values.public_child_sources,
                        ".",
                        "//content")

  configs += [
    "//build/config/compiler:wexit_time_destructors",
    "//content:content_implementation",
  ]

  deps = [
    "//content/child",  # Must not be public_deps!
    "//content/public/common:common_sources",
    "//gin",
  ]

  allow_circular_includes_from = [
    # This target is a pair with content/child. They always go together and
    # include headers from each other.
    "//content/child",
  ]
}
