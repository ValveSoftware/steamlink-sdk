# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ppapi/ppapi_sources.gni")

# Link to this target to get the PPAPI C++ wrapper objects and plugin startup
# code. See also "objects" below.
source_set("cpp") {
  sources = [
    "module_embedder.h",
    "ppp_entrypoints.cc",
  ]

  public_deps = [
    ":objects",
    "//ppapi/c",
  ]
}

# Link to this target to get only the PPAPI C++ wrapper objects but not the
# plugin startup code. Some plugins need special startup code that they supply
# themselves.
source_set("objects") {
  sources = rebase_path(ppapi_sources.cpp_source_files, ".", "..")
  configs += [ "//build/config:precompiled_headers" ]
}
