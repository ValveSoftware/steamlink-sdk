# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../mojo_application.gni")

source_set("tests") {
  testonly = true

  sources = [
    "array_common_test.h",
    "array_unittest.cc",
    "associated_interface_unittest.cc",
    "bind_task_runner_unittest.cc",
    "binding_callback_unittest.cc",
    "binding_unittest.cc",
    "buffer_unittest.cc",
    "connector_unittest.cc",
    "constant_unittest.cc",
    "container_test_util.cc",
    "container_test_util.h",
    "equals_unittest.cc",
    "handle_passing_unittest.cc",
    "interface_ptr_unittest.cc",
    "map_common_test.h",
    "map_unittest.cc",
    "message_queue.cc",
    "message_queue.h",
    "multiplex_router_unittest.cc",
    "request_response_unittest.cc",
    "router_test_util.cc",
    "router_test_util.h",
    "router_unittest.cc",
    "sample_service_unittest.cc",
    "serialization_warning_unittest.cc",
    "stl_converters_unittest.cc",
    "string_unittest.cc",
    "struct_unittest.cc",
    "sync_method_unittest.cc",
    "type_conversion_unittest.cc",
    "union_unittest.cc",
    "validation_context_unittest.cc",
    "validation_unittest.cc",
    "variant_test_util.h",
  ]

  deps = [
    ":mojo_public_bindings_test_utils",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//mojo/public/cpp/test_support:test_utils",
    "//mojo/public/interfaces/bindings/tests:test_associated_interfaces",
    "//mojo/public/interfaces/bindings/tests:test_interfaces",
    "//mojo/public/interfaces/bindings/tests:test_interfaces_experimental",
    "//mojo/public/interfaces/bindings/tests:test_struct_traits_interfaces",
    "//testing/gtest",
  ]

  data = [
    "//mojo/public/interfaces/bindings/tests/data/validation/",
  ]

  if (is_ios) {
    assert_no_deps = [ "//third_party/WebKit/*" ]
  } else {
    sources += [
      "pickle_unittest.cc",
      "struct_traits_unittest.cc",
    ]

    deps += [ "//mojo/public/interfaces/bindings/tests:test_interfaces_blink" ]
  }
}

if (!is_ios) {
  source_set("for_blink_tests") {
    testonly = true

    sources = [
      "array_common_test.h",
      "container_test_util.cc",
      "container_test_util.h",
      "map_common_test.h",
      "variant_test_util.h",
      "wtf_array_unittest.cc",
      "wtf_map_unittest.cc",
      "wtf_types_unittest.cc",
    ]

    deps = [
      "//mojo/public/cpp/bindings",
      "//mojo/public/cpp/system",
      "//mojo/public/interfaces/bindings/tests:test_interfaces",
      "//mojo/public/interfaces/bindings/tests:test_wtf_types",
      "//mojo/public/interfaces/bindings/tests:test_wtf_types_blink",
      "//testing/gtest",
    ]
  }
}

source_set("struct_with_traits_impl") {
  sources = [
    "struct_with_traits_impl.cc",
    "struct_with_traits_impl.h",
  ]

  deps = [
    "//base",
    "//mojo/public/cpp/system:system",
  ]
}

source_set("perftests") {
  testonly = true

  sources = [
    "bindings_perftest.cc",
  ]

  if (!is_ios) {
    sources += [ "e2e_perftest.cc" ]
  }

  deps = [
    "//base/test:test_support",
    "//mojo/edk/test:test_support",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//mojo/public/cpp/test_support:test_utils",
    "//mojo/public/interfaces/bindings/tests:test_interfaces",
    "//testing/gtest",
  ]
}

source_set("mojo_public_bindings_test_utils") {
  sources = [
    "validation_test_input_parser.cc",
    "validation_test_input_parser.h",
  ]

  deps = [
    "//mojo/public/c/system",
  ]
}
